resource "aws_cognito_user_pool" "main" {
  name                     = "${local.name_prefix}-users"
  user_pool_tier           = "ESSENTIALS"
  username_attributes      = ["email"]
  auto_verified_attributes = ["email"]
  mfa_configuration        = "OFF"

  sign_in_policy {
    allowed_first_auth_factors = ["EMAIL_OTP", "PASSWORD"]
  }

  lambda_config {
    pre_token_generation_config {
      lambda_arn     = aws_lambda_function.admin_claims.arn
      lambda_version = "V2_0"
    }
  }

  username_configuration {
    case_sensitive = false
  }

  email_configuration {
    email_sending_account = "COGNITO_DEFAULT"
  }
}

resource "aws_cognito_user_pool_domain" "main" {
  domain       = var.cognito_domain_prefix
  user_pool_id = aws_cognito_user_pool.main.id
}

resource "aws_cognito_identity_provider" "google" {
  user_pool_id  = aws_cognito_user_pool.main.id
  provider_name = "Google"
  provider_type = "Google"

  provider_details = {
    authorize_scopes = "openid email profile"
    client_id        = var.google_client_id
    client_secret    = var.google_client_secret
  }

  attribute_mapping = {
    email          = "email"
    email_verified = "email_verified"
    given_name     = "given_name"
    family_name    = "family_name"
  }
}

resource "aws_cognito_user_pool_client" "web" {
  name         = "${local.name_prefix}-web"
  user_pool_id = aws_cognito_user_pool.main.id

  generate_secret = false
  explicit_auth_flows = [
    "ALLOW_REFRESH_TOKEN_AUTH"
  ]

  allowed_oauth_flows_user_pool_client = true
  allowed_oauth_flows                  = ["code"]
  allowed_oauth_scopes                 = ["openid", "email", "profile"]
  callback_urls                        = local.callback_urls
  logout_urls                          = local.logout_urls
  supported_identity_providers         = [aws_cognito_identity_provider.google.provider_name]
  prevent_user_existence_errors        = "ENABLED"
}

locals {
  cloudfront_domains = [
    for domain in concat(
      tolist(aws_cloudfront_distribution.frontend.aliases),
      [aws_cloudfront_distribution.frontend.domain_name]
    ) : "https://${domain}"
  ]
  callback_urls = sort(concat(local.cloudfront_domains, ["https://localhost:5173/"]))
  logout_urls   = sort(concat(local.cloudfront_domains, ["https://localhost:5173/"]))
}
